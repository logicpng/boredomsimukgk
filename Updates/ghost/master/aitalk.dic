//test edit

OnAiTalk
{
	if isSwitching == 1; return //end function immediately if switching is currently happening

	if isRandTalkInterval == 1; OnRandTalkInterval //in menu_sakura.dic

	//post epilogue random switching
	//we rolled the dice right and epilogue is finished, time to select who to switch to
	//also no countdown for aldebaran
	if RAND(15) == 0 && mainConvos[0] == 4 && alCount == ""
	{
		"\t"
		--
		//if the fronter isn't already aldebaran, roll another die to make it a rarer occurence
		if scheme != 3 && RAND(4) == 0; "\![embed,OnSwitch,Aldebaran]"
		else
		{
			//if the dice roll fails or the fronter is already aldebaran, switch to rigel if the fronter isn't rigel, or to vega, if the fronter isn't vega
			if scheme != 1; "\![embed,OnSwitch,Rigel]"
			elseif scheme != 2; "\![embed,OnSwitch,Vega]"
			//on the freak chance it's stuck in Please Wait... scheme or it equals something else
			else; RandomTalk + OnEmbedExtraCheck
		}
	}
	else
	{
		if alCount != "" //generated in OnSwitch if switch to al happens, can take values from 0 to 3
		{
			if scheme != 3
			{
				ERASEVAR("alCount") //if current aster isn't aldebaran this shouldn't even be here
				RandomTalk
			} 
			else //scheme set to aldebaran
			{
				if alCount <= 0 //coundown on 0 or less, countdown done, do the switch and skip random talk
				{
					"\t"
					--
					ERASEVAR("alCount") //don't need this anymore
					"\![embed,OnSwitch,Rigel]" //let shiori pick either or as random candidates
					"\![embed,OnSwitch,Vega]"
				}
				elseif alCount > 0 //countdown not done, show a random (aldebaran only eventually) line and decrease the countdown
				{
					RandomTalk + OnEmbedExtraCheck
					alCount--
				}
			}
		}
		//else; RandomTalk //variable alCount doesn't exist, proceed with just randomtalk
		else; RandomTalk + OnEmbedExtraCheck //variable alCount doesn't exist, proceed with just randomtalk
		// stickyWindow = 1 //flag to wipe out later, check boot.dic
		// --
		// //"\![reset,sticky-window]"
	}
	
}

OnEmbedExtraCheck
{
	if extraflag == 1; "\w8\w8"
	else; ""
	
}

//will probably be eventgually modified to be separated per current fronter, but there's not enough lines to do this without being annoyed very fast
RandomTalk
{
	if CHAIN.IDName != ""
	{
		LastTalk = ChainTalk
	}
	else
	{
		//not accounting for 4 because it should be infeasible to get a random dialogue during it
		case scheme
		{
			when 1; LastTalk = OnRandomAltairTalk
			when 2; LastTalk = OnRandomVegaTalk
			when 3; LastTalk = OnRandomAlTalk
		}
	}
	"%(LastTalk)"
}

//switching between aster modules, done through internal dressups
OnSwitch
{
	SHIORI3FW.EndChainTalk //end any chain talks
	_display = ""
	_module = TOLOWER(reference0)
	_code = -1

	case _module
	{
		when "rigel"; _code = 1
		when "vega";  _code = 2
		when "al";  _code = 3
		when "aldebaran"
		{
			_code = 3
			alCount = RAND(4) //a countdown for aldebaran to switch back into either rigel or vega, for in-universe instablity reasons. see OnAiTalk
		}  
	}

	if _code != -1 && _code != scheme
	{
		_display = "\![notify,OnSwitchInProgress,start]\i[1]\![theme,switch]\_w[1500]"

		case _module
		{
			when "rigel"; _display += "\![theme,rigel]"
			when "vega";  _display += "\![theme,vega]"
			when "al";  _display += "\![theme,aldebaran]"
			when "aldebaran";  _display += "\![theme,aldebaran]"
		}
		
		_display += "\_w[500]"

	}

	_display += "\![notify,OnSwitchInProgress,end]"
	_display
}

OnSwitchInProgress
{
	if reference0 == "start"; isSwitching = 1
	else; isSwitching = 0
}

//press X
OnTest: all
{
	//"\_q\f[bold,1]Talk count:\f[bold,0]\n[150]\![*] Rigel: \f[bold,1]%(OUTPUTNUM('OnRandomAltairTalk'))\f[bold,0]\n\![*] Vega: \f[bold,1]%(OUTPUTNUM('OnRandomVegaTalk'))\f[bold,0]\n\![*] Aldebaran: \f[bold,1]%(OUTPUTNUM('OnRandomAlTalk'))\f[bold,0]"
	//"\_q{urs3,a}{rigel2,b}{vega1,c}{al0,d}"
	//OnRandomAltairTalk
}

